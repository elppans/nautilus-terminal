# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Elppans D. Elven <elppans@...>

pkgname=nautilus-terminal
pkgver=4.1.0+4+g177fc1d
pkgrel=1
pkgdesc="Embedded terminal for Nautilus (Fork)"
arch=(any)
url="https://github.com/flozz/nautilus-terminal"
license=(GPL3)
depends=('nautilus>=46' 'python-nautilus>=4' python-psutil vte3) # Alterado versão mínima do nautilus e python-nautilus
makedepends=(git python-setuptools)
_commit=177fc1dd579520b6b076545220ff48362bce47fb # Atualizado diretório "extensions-3.0" para "extensions-4"
source=("git+https://github.com/elppans/nautilus-terminal#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  python3 setup.py build
}

package() {
  cd $pkgname
  python3 setup.py install --root="$pkgdir" --optimize=1
  rm "$pkgdir"/usr/share/glib-2.0/schemas/gschemas.compiled
}
